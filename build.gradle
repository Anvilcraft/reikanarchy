buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "1.0"
group = "Reika.DragonAPI"
archivesBaseName = "DragonAPI"

configurations {
    embedded
    implementation.extendsFrom(embedded)
}

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

repositories {
    maven { url = "https://maven.tilera.xyz" }
    maven {
        url = "https://s3.tilera.xyz/cdn/minecraft/libs/"
        metadataSources { artifact() }
    }
    maven {
        name "central"
        url "https://maven.thorfusion.com/artifactory/central/"
    }
}

dependencies {
    // Reika mods
    compileOnly "reika:ChromatiCraft:V33a:deobf"
    compileOnly "reika:CritterPet:V33a:deobf"
    compileOnly "reika:GeoStrata:V33a:deobf"
    compileOnly "reika:LegacyCraft:V33a:deobf"
    compileOnly "reika:MeteorCraft:V33a:deobf"
    compileOnly "reika:ReactorCraft:V33a:deobf"
    compileOnly "reika:RotaryCraft:V33a:deobf"
    compileOnly "reika:Satisforestry:V33a:deobf"
    compileOnly "reika:VoidMonster:V33a:deobf"
    compileOnly "reika:CondensedOres:V33a"

    // Direct deps (reika, WTF)
    compileOnly "mods:MineFactoryReloaded:[1.7.10]2.8.0-104:dev"
    compileOnly "appeng:appliedenergistics2:rv3-beta-21:dev"
    compileOnly "buildcraft:buildcraft:7.1.25:dev"
    compileOnly "chisel:Chisel:2.9.5.11:deobf"
    compileOnly "codechicken:CodeChickenCore:1.7.10-1.0.7.48:dev"
    compileOnly "codechicken:CodeChickenLib:1.7.10-1.1.3.141:dev"
    compileOnly "codechicken:ForgeMultipart:1.7.10-1.2.0.347:dev"
    compileOnly "codechicken:NotEnoughItems:1.7.10-1.0.5.120:dev"
    compileOnly "cofh:CoFHCore:[1.7.10]3.1.4-329:dev"
    compileOnly "cofh:ThermalExpansion:[1.7.10]4.1.5-248:dev"
    compileOnly "dan200:ComputerCraft:1.75:deobf"
    compileOnly "mcp.mobius.waila:Waila:1.5.10_1.7.10:dev"
    compileOnly "mekanism:Mekanism:1.7.10-9.13.1:deobf"
    compileOnly "mods:AdvancedRocketry:1.7.10-1.0.19b:deobf"
    embedded "mods:AgriCraft:1.7.10-1.5.0:api"
    compileOnly "mods:BiomesOPlenty:1.7.10-2.1.0.1889:deobf"
    compileOnly "mods:BloodMagic:1.7.10-1.3.3-17:deobf"
    compileOnly "mods:BluePower:1.7.10-0.2.962:universal"
    compileOnly "mods:Botania:r1.8-249:deobf"
    compileOnly "mods:CarpentersBlocks:v3.3.8.2:deobf"
    compileOnly "mods:ClimateControl:0.8.2:deobf"
    compileOnly "mods:Framez:1.7.10-0.2-32-hotfix:deobf"
    compileOnly "mods:GalacticraftCore:1.7-3.0.12.504:deobf"
    compileOnly "mods:HEXCraft:1.7.10-0.13.2:deobf"
    compileOnly "mods:ImmersiveEngineering:0.7.7:deobf"
    compileOnly "mods:MatterOverdrive:1.7.10-0.4.2:deobf"
    compileOnly "mods:MineChem:1.7.10:deobf"
    compileOnly "mods:MineTweaker3:1.7.10-3.0.10B:dev"
    compileOnly "mods:OpenComputers:MC1.7.10-1.8.3+089dd28:deobf"
    compileOnly "mods:PneumaticCraft:1.7.10-1.12.7-152:deobf"
    compileOnly "mods:ProjectE:1.7.10-PE1.10.1:deobf"
    compileOnly "mods:ProjectRed:1.7.10-4.7.0pre8.92:dev"
    compileOnly "mods:Railcraft_1.7.10:9.12.2.0"
    compileOnly "mods:StorageDrawers:1.7.10-1.10.9:deobf"
    compileOnly "mods:Streams:1.7.10-0.3.4:deobf"
    compileOnly "mods:TConstruct:1.7.10-1.8.8:deobf"
    compileOnly "mods:TerraFirmaCraft:0.79.30.925:deobf"
    compileOnly "mods:arsmagica2:1.4.0.009:deobf"
    compileOnly "mods:binnie-mods:1.7.10-2.0.22.7:deobf"
    compileOnly "mods:forestry_1.7.10:4.2.16.64"
    compileOnly "mods:mystcraft:1.7.10-0.12.3.04:dev"
    compileOnly "mods:twilightforest:1.7.10-2.3.8:deobf"
    compileOnly "net.industrial-craft:industrialcraft-2:2.2.827-experimental:deobf"
    compileOnly "net.machinemuse:ModularPowersuits:1.7.10-0.11.1.0:deobf"
    compileOnly "thaumcraft:Thaumcraft:1.7.10-4.2.3.5:deobf"
    compileOnly "universalelectricity:universalelectricity:5.1.0:deobf"

    // Dependency deps
    compileOnly "mods:Numina:0.4.1.106"
    compileOnly "mods:MrTJPCore:1.1.0.31:dev"
    compileOnly "mods:ForgeRelocation:0.0.1.4:dev"
}

jar {
    from (configurations.embedded.collect { it.isDirectory() ? it : zipTree(it) }) {
       duplicatesStrategy = 'exclude'
       exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/services/*.Processor', 'META-INF/versions/**'
    }

    manifest {
        attributes([
                "FMLCorePlugin": "Reika.DragonAPI.Auxiliary.DragonAPIASMHandler",
                "FMLCorePluginContainsFMLMod": "false",
                "FMLAT": "DragonAPI_at.cfg"
        ])
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching('mcmod.info') {
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'

    manifest {
        attributes([
                "FMLCorePlugin": "Reika.DragonAPI.Auxiliary.DragonAPIASMHandler",
                "FMLCorePluginContainsFMLMod": "false",
                "FMLAT": "DragonAPI_at.cfg"
        ])
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName

            artifact deobfJar
            artifact sourcesJar
            artifact jar
        }
    }

    repositories {
        if (project.hasProperty('mvnURL')) {
                maven {
                    credentials {
                        username findProperty("mvnUsername")
                        password findProperty("mvnPassword")
                    }
                    url = findProperty("mvnURL")
                }
        }
        else {
                mavenLocal()
        }
    }
}
